plugins {
	id 'fabric-loom' version '1.6-SNAPSHOT'
	id "com.modrinth.minotaur" version "2.+"
}

dependencies {
	minecraft libs.minecraft
	mappings libs.mappings

	modImplementation libs.fabric.loader
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

loom {
	accessWidenerPath = file("src/main/resources/hswf3.accesswidener")
}

java {
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

jar {
	sourceSets {
		main {
			java {
				exclude "com/fileofname/hswf3/HSWF3Mod.java" //Currently unused so exclude from build
			}
		}
	}
	from("LICENSE") {
		rename { "${it}_${project.base.archivesName.get()}"}
	}
}

modrinth {
	token = System.getenv("MODRINTH_TOKEN")
	projectId = "hswf3"
	versionType = findProperty("versionType")?.allWhitespace ? DEFAULT_VERSION_TYPE : findProperty("versionType")
	changelog = findProperty("changelog")?.allWhitespace ? DEFAULT_CHANGELOG : findProperty("changelog")
	uploadFile = remapJar
	syncBodyFrom = rootProject.file("README.md").text
	loaders = ["fabric", "quilt"]
	debugMode = findProperty("modrinthDebug")?.allWhitespace ? false : findProperty("modrinthDebug")?.toBoolean()
}

tasks.modrinth.dependsOn(tasks.modrinthSyncBody)